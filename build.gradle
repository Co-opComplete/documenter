buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath group: 'com.scuilion.gradle', name: 'utils', version: '0.+'
    }
}

apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'eclipse'

eclipse {
    classpath {
        defaultOutputDir = file('build-eclipse')
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

group = 'com.scuilion.documenter'
version = '0.1'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime

    integrationTestCompile.extendsFrom compile
    integrationTestRuntime.extendsFrom runtime
}

dependencies{
    compile 'commons-io:commons-io:2.4'
    compile 'javassist:javassist:3.12.1.GA'
    testCompile 'junit:junit:4.12'
    //testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'org.powermock:powermock-core:1.6.1'
    testCompile 'org.powermock:powermock-module-junit4:1.6.1'
    testCompile 'org.powermock:powermock-api-mockito:1.6.1'

    integrationTestRuntime files("${System.properties['java.home']}/../lib/tools.jar") 
}

sourceSets {
    integrationTest {
        java {
            srcDir 'src/integrationTest/java'
        }
        resources {
            srcDir 'src/integrationTest/resources'
        }
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + sourceSets.main.output + configurations.testRuntime
    }
}

task integrationTest(type:Test){
    dependsOn publishToMavenLocal
    dependsOn integrationTestClasses

    systemProperties['version'] = version

    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath + sourceSets.main.output + configurations.integrationTestCompile + sourceSets.test.output

    getReports().getHtml().setDestination(new File("$project.reporting.baseDir", 'integrationTest'))
    getReports().getJunitXml().setDestination(new File("$project.buildDir", 'integration-results')) 
}

apply plugin: 'utils'


