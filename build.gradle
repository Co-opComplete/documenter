buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath group: 'com.scuilion.gradle', name: 'utils', version: '0.+'
    }
}

apply plugin: 'maven-publish'
apply plugin: 'java'

repositories {
    mavenCentral()
    mavenLocal()
}


group = 'com.scuilion.documenter'
version = '0.1'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime

    integrationTestCompile.extendsFrom compile
    integrationTestRuntime.extendsFrom runtime
}

dependencies{
    compile 'com.programmaticallyspeaking.aptdemo:apt-demo:1.0-SNAPSHOT'
    testCompile 'junit:junit:4.11'
    integrationTestRuntime files("${System.properties['java.home']}/../lib/tools.jar") 
}
sourceSets {
    integrationTest {
        java {
            srcDir 'src/integrationTest/java'
        }
        resources {
            srcDir 'src/integrationTest/resources'
        }
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + sourceSets.main.output + configurations.testRuntime
    }
}

task integrationTest(type:Test){
    dependsOn publishToMavenLocal
    dependsOn integrationTestClasses

    systemProperties['integrationTest.location'] = new File(sourceSets.integrationTest.output.resourcesDir, 'projectRoot').absolutePath
    systemProperties['version'] = version

    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath + sourceSets.main.output + configurations.integrationTestCompile + sourceSets.test.output

    getReports().getHtml().setDestination(new File("$project.reporting.baseDir", 'integrationTest'))
    getReports().getJunitXml().setDestination(new File("$project.buildDir", 'integration-results')) 
}

apply plugin: 'utils'

